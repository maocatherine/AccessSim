buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
        jcenter()
    }
}

plugins {
  id "com.jfrog.bintray" version "1.8.4"
  id "com.jfrog.artifactory" version "4.8.1"
  id "maven-publish"
}

dependencies {
    compile 'edu.kit.ifv.mobitopp:actitopp:0.1',
            'net.sf.opencsv:opencsv:2.3',
            'org.apache.commons:commons-compress:1.12',
            'org.yaml:snakeyaml:1.18'
    
    testCompile 'org.junit.jupiter:junit-jupiter-api:5.4.0',
                'org.junit.jupiter:junit-jupiter-params:5.4.0',
                'org.junit.jupiter:junit-jupiter-migrationsupport:5.4.0',
                'org.hamcrest:hamcrest-all:1.3',
                'com.github.npathai:hamcrest-optional:1.0',
                'org.mockito:mockito-core:2.24.0',
                'nl.jqno.equalsverifier:equalsverifier:2.2.2'
        
    testRuntime 'org.junit.jupiter:junit-jupiter-engine:5.4.0',
                'org.junit.vintage:junit-vintage-engine:5.4.0'
}

tasks.withType(JavaCompile) { 
    options.compilerArgs << "-Xlint:all" 
}

eclipse.classpath {
    downloadSources=true
}

sourceSets {
    integrationTest {
        compileClasspath += sourceSets.test.runtimeClasspath
        runtimeClasspath += sourceSets.test.runtimeClasspath
        java.srcDirs = ['src/integration-test/java']
        resources.srcDirs = ['src/integration-test/resources']
    }
}

test {
    useJUnitPlatform()
    enableAssertions = "true"
    maxHeapSize = "2G"
    testLogging.showStandardStreams = true
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

publishing {
    publications {
        mavenPublication(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
            groupId project.group
            artifactId project.name
            version project.version
        }
    }
    repositories {
        maven {
            credentials {
                username getPropertyOrDefault('repositoryUsername')
                password getPropertyOrDefault('repositoryPassword')
            }
            def repositoryUrl = getPropertyOrDefault('repositoryUrl')
            def releasesRepoUrl = "${repositoryUrl}/repository/maven-releases/"
            def snapshotsRepoUrl = "${repositoryUrl}/repository/maven-snapshots"
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
        }
    }
}

artifactory {
    contextUrl = 'http://oss.jfrog.org'
    publish {
        repository {
            repoKey = 'oss-snapshot-local'
            username = project.hasProperty('user') ? project.property('user') : 'user'
            password = project.hasProperty('key') ? project.property('key') : 'key'
        }
        defaults {
            publications('mavenPublication')
            publishArtifacts = true
            publishPom = true
        }
    }
    resolve {
        repoKey = 'jcenter'
    }
    def buildnumber = project.hasProperty('build.number') ? project.property('build.number') : '' 
    clientConfig.info.setBuildNumber(buildnumber)
}

bintray {
    user = project.hasProperty('user') ? project.property('user') : 'user'
    key = project.hasProperty('key')? project.property('key') : 'key'
    publications = ['mavenPublication']
    pkg {
        repo = 'ifv-mobitopp'
        name = project.name
        userOrg = 'ifv-mobitopp'
        licenses = ['GPL-3.0']
        vcsUrl = 'https://github.com/ifv-mobitopp/mobitopp'
        version {
          name = project.version
          description = project.version
          released = new Date()
        }
    }
}

uploadArchives {
    repositories {
        def repositoryUrl = getPropertyOrDefault('repositoryUrl')
        def repositoryUsername = getPropertyOrDefault('repositoryUsername')
        def repositoryPassword = getPropertyOrDefault('repositoryPassword')
        mavenDeployer {
            repository(url: "${repositoryUrl}/repository/maven-releases/") {
                authentication(userName: repositoryUsername, password: repositoryPassword)
            }
            snapshotRepository(url: "${repositoryUrl}/repository/maven-snapshots") {
                authentication(userName: repositoryUsername, password: repositoryPassword)
            }
        }
    }
}

def getPropertyOrDefault(key) {
    return project.hasProperty(key) ? project.property(key) : key
}