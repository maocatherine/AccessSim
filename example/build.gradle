dependencies {
    compile project(':mobitopp')
    
    testCompile 'org.junit.jupiter:junit-jupiter-api:5.4.0',
                'org.junit.jupiter:junit-jupiter-params:5.4.0',
                'org.hamcrest:hamcrest-all:1.3',
                'org.mockito:mockito-core:2.24.0'
    
    testRuntime 'org.junit.jupiter:junit-jupiter-engine:5.4.0'
}

test {
    useJUnitPlatform()
}

task runPopulationSynthesis(type: JavaExec) {
    args = ['config/leopoldshafen/population-synthesis.yaml']
    main = "edu.kit.ifv.mobitopp.populationsynthesis.PopulationSynthesisExample"
}

task runSimulation(type: JavaExec, dependsOn: compileJava) {
    args = ['config/leopoldshafen/simulation.yaml']
    main = "edu.kit.ifv.mobitopp.simulation.SimulationExample"
}

tasks.withType(JavaExec) {
    classpath = sourceSets.main.runtimeClasspath
    dependsOn check
    enableAssertions = "true"
    group = "application"
}

distributions {
    main {
        contents {
            into('dist') {
                from jar
                into('lib') {
                    from(project.configurations.runtime)
                }
            }
            into('bin') {
                from('bin.run') {
                    fileMode 0755
                    include 'run*.sh'
                }
            }
            into('config') {
                from('config') {
                    include '*.txt'
                    include 'TargetParameterRepetition2.csv'
                    include '*.yaml'
                }
            }
            into('') {
                from { "$buildDir/dist" }
            }
        }
    }
}